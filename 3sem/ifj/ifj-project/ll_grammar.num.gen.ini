01 EXPR_OR_CALL ::= EXPR
02 EXPR_OR_CALL ::= tt_ident CALL
03 EXPR_OPT ::= EXPR
04 EXPR_OPT ::= ''
05 FILE ::= STMT_LIST tt_EOF
06 STMT_LIST ::= ''
07 STMT_LIST ::= STMT STMT_LIST
08 TYPE ::= tt_kw_Double
09 TYPE ::= tt_kw_Double_opt
10 TYPE ::= tt_kw_Int
11 TYPE ::= tt_kw_Int_opt
12 TYPE ::= tt_kw_String
13 TYPE ::= tt_kw_String_opt
14 TYPE ::= tt_kw_Bool
15 TYPE ::= tt_kw_Bool_opt
16 STMT ::= STMT_FUNC_DEF
17 STMT ::= STMT_LET
18 STMT ::= STMT_VAR
19 STMT ::= tt_ident STMT_IDENT
20 STMT ::= STMT_IF
21 STMT ::= STMT_WHILE
22 STMT ::= STMT_RETURN
23 STMT_FUNC_DEF ::= tt_kw_func tt_ident tt_left_paren PARAM_LIST tt_right_paren RETURN_TYPE tt_left_curly STMT_LIST tt_right_curly
24 PARAM_LIST ::= ''
25 PARAM_LIST ::= PARAM PARAM_REST
26 PARAM_REST ::= tt_comma PARAM PARAM_REST
27 PARAM_REST ::= ''
28 PARAM ::= OPT_IDENT OPT_IDENT tt_colon TYPE
29 OPT_IDENT ::= tt_ident
30 OPT_IDENT ::= tt_underscore
31 RETURN_TYPE ::= ''
32 RETURN_TYPE ::= tt_arrow TYPE
33 STMT_LET ::= tt_kw_let tt_ident DECL
34 STMT_VAR ::= tt_kw_var tt_ident DECL
35 DECL ::= tt_colon TYPE DEF
36 DECL ::= tt_assign EXPR_OR_CALL
37 DEF ::= tt_assign EXPR_OR_CALL
38 DEF ::= ''
39 STMT_IDENT ::= tt_assign EXPR_OR_CALL
40 STMT_IDENT ::= CALL
41 CALL ::= tt_left_paren ARGUMENT_LIST tt_right_paren
42 ARGUMENT_LIST ::= ''
43 ARGUMENT_LIST ::= ARGUMENT ARGUMENT_REST
44 ARGUMENT_REST ::= tt_comma ARGUMENT ARGUMENT_REST
45 ARGUMENT_REST ::= ''
46 ARGUMENT ::= tt_ident OPT_TERM
47 ARGUMENT ::= LITERAL
48 OPT_TERM ::= ''
49 OPT_TERM ::= tt_colon TERM
50 TERM ::= tt_ident
51 TERM ::= LITERAL
52 LITERAL ::= tt_lit_Int
53 LITERAL ::= tt_lit_Double
54 LITERAL ::= tt_lit_String
55 LITERAL ::= tt_lit_Bool
56 LITERAL ::= tt_kw_nil
57 STMT_IF ::= tt_kw_if IF_CONDITION tt_left_curly STMT_LIST tt_right_curly tt_kw_else tt_left_curly STMT_LIST tt_right_curly
58 IF_CONDITION ::= EXPR
59 IF_CONDITION ::= tt_kw_let tt_ident
60 STMT_WHILE ::= tt_kw_while EXPR tt_left_curly STMT_LIST tt_right_curly
61 STMT_RETURN ::= tt_kw_return EXPR_OPT
