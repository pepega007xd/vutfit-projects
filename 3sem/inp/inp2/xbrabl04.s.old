; Autor reseni: Tomas Brablec xbrabl04
; Pocet cyklu k serazeni puvodniho retezce: 1710
; Pocet cyklu razeni sestupne serazeneho retezce: 2326
; Pocet cyklu razeni vzestupne serazeneho retezce: 376
; Pocet cyklu razeni retezce s vasim loginem: 446
; Implementovany radici algoritmus: Insertion sort
; ------------------------------------------------

; DATA SEGMENT
                .data

; login:          .asciiz "vitejte-v-inp-2023"    ; puvodni uvitaci retezec
; login:          .asciiz "vvttpnjiiee3220---"  ; sestupne serazeny retezec
; login:          .asciiz "---0223eeiijnpttvv"  ; vzestupne serazeny retezec
login:          .asciiz "xbrabl04"            ; SEM DOPLNTE VLASTNI LOGIN
                                                ; A POUZE S TIMTO ODEVZDEJTE

params_sys5:    .space  8   ; misto pro ulozeni adresy pocatku
                            ; retezce pro vypis pomoci syscall 5
                            ; (viz nize - "funkce" print_string)

; CODE SEGMENT
                .text
main:
        ; SEM DOPLNTE VASE RESENI

        ; this code is implemented using assembly
        ; it's a simple insertion sort, without checking for NULL ptr
        ; should be fast enough for short strings

        ; void insertionSort2(char *str) {
        ;     for (char *idx = str; *idx != 0; idx++) {
        ;         for (char *insert = idx; insert != str; insert--) {
        ;             char *prev = insert - 1;
        ;             if (*prev > *insert) {
        ;                 char tmp = *prev;
        ;                 *prev = *insert;
        ;                 *insert = tmp;
        ;             } else {
        ;                 break;
        ;             }
        ;         }
        ;     }
        ; }

        daddi   r30, r0, login ; r30 is char* str
        daddi   r1, r0, login ; r1 is char* idx

outer_for:
        lb      r2, 0(r1) ; r2 is char at idx
        beqz    r2, outer_end

        dadd    r3, r1, r0 ; r3 is char* insert
inner_for:
        beq     r3, r30, inner_end

        lb      r4, -1(r3) ; r4 is char at prev
        lb      r5, 0(r3) ; r5 is char at insert
        dsub    r6, r5, r4 ; *prev - *insert
        bgez    r6, inner_end ; "else" branch

        ; "if" branch
        sb      r4, 0(r3) ; prev -> insert
        sb      r5, -1(r3) ; insert -> prev

        daddi   r3, r3, -1 ; insert--
        b       inner_for ; end of inner_for

inner_end:
        daddi   r1, r1, 1 ; idx++
        b       outer_for ; end of outer_for

outer_end:
        daddi   r4, r0, login   ; vozrovy vypis: adresa login: do r4
        jal     print_string    ; vypis pomoci print_string - viz nize

        syscall 0   ; halt


print_string:   ; adresa retezce se ocekava v r4
                sw      r4, params_sys5(r0)
                daddi   r14, r0, params_sys5    ; adr pro syscall 5 musi do r14
                syscall 5   ; systemova procedura - vypis retezce na terminal
                jr      r31 ; return - r31 je urcen na return address
